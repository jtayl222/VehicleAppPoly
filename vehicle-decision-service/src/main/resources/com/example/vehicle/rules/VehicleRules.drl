package com.example.vehicle.rules;
unit VehicleUnitData;

import com.example.vehicle.datamodel.Car;
import com.example.vehicle.datamodel.Truck;

rule "Car Rule using polymorphism"
  when
    $v : /vehicleInstances[overloaded == false, (this instanceof Car), this#Car.currentPassengers > this#Car.maxPassengers ]
  then
    modify($v){setOverloaded(true)};
end

rule "Truck Rule using polymorphism"
  when
    $v : /vehicleInstances[overloaded == false, (this instanceof Truck), this#Truck.currentCargoWeight > this#Truck.maxCargoWeight]
  then
    modify($v){setOverloaded(true)};
end

query "GetOverloadedVehicles"
  $v: /vehicleInstances[overloaded]
end
